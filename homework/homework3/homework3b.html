
<!DOCTYPE html>
<html lang="en">
	<head> 
		<title>Homework3B - mjloyola</title>
		<meta charset="utf-8">
		<style>
			body {
			  margin: 0px;
				overflow: hidden;
			}
		</style>
	</head>
	<body>

	<div id="container"></div>
    <script src="js/three.js"></script>

    <!--Vertex Shader functions as normal-->
    <script id="vertexShader" type="x-shader/x-vertex">

		uniform mat4 modelMatrix;
		uniform mat4 viewMatrix;
	    uniform mat4 projectionMatrix;

	    attribute vec3 position;
		attribute vec2 uv;
		attribute vec3 normal;
		attribute vec4 AttrMultiTexCoord0;

		varying vec2 vUV;
		varying vec4 VaryingTexCoord[1];

	    precision mediump float;

		void main() {
	       	vUV = uv;
			VaryingTexCoord[0] = AttrMultiTexCoord0;
	       	gl_Position = projectionMatrix  * viewMatrix * modelMatrix  * vec4( position, 1.0 );
	    }
	</script>

	<!--Fragment Shader renders everything in the window-->
	<script id="fragmentShader" type="x-shader/x-fragment">

		precision mediump float;

		uniform vec2 u_resolution;
		uniform float iTime;
		uniform sampler2D tex;
		varying vec2 vUV;
		varying vec4 VaryingTexCoord[1];

		const int MAX_MARCHING_STEPS = 255;
		const float MIN_DIST = 0.0;
		const float MAX_DIST = 100.0;
		const float EPSILON = 0.0001;

		//Rotation matrix around the X axis.
		mat3 rotateX(float theta) {
			float c = cos(theta);
			float s = sin(theta);
			return mat3(
				vec3(1, 0, 0),
				vec3(0, c, -s),
				vec3(0, s, c)
			);
		}
		//Rotation matrix around the Y axis.
		mat3 rotateY(float theta) {
			float c = cos(theta);
			float s = sin(theta);
			return mat3(
				vec3(c, 0, s),
				vec3(0, 1, 0),
				vec3(-s, 0, c)
			);
		}
		//Rotation matrix around the Z axis.
		mat3 rotateZ(float theta) {
			float c = cos(theta);
			float s = sin(theta);
			return mat3(
				vec3(c, -s, 0),
				vec3(s, c, 0),
				vec3(0, 0, 1)
			);
		}

		//Constructive solid geometry intersection operation on SDF-calculated distances.
		float intersectSDF(float distA, float distB) {
			return max(distA, distB);
		}
		//Constructive solid geometry union operation on SDF-calculated distances.
		float unionSDF(float distA, float distB) {
			return min(distA, distB);
		}
		//Constructive solid geometry difference operation on SDF-calculated distances.
		float differenceSDF(float distA, float distB) {
			return max(distA, -distB);
		}
		
		//Signed distance function for a cube centered at the origin
		float boxSDF(vec3 p, vec3 size) {
			vec3 d = abs(p) - (size / 2.0);
			
			// Assuming p is inside the cube, how far is it from the surface?
			// Result will be negative or zero.
			float insideDistance = min(max(d.x, max(d.y, d.z)), 0.0);
			
			// Assuming p is outside the cube, how far is it from the surface?
			// Result will be positive or zero.
			float outsideDistance = length(max(d, 0.0));
			
			return insideDistance + outsideDistance;
		}

		//Signed distance function for a sphere centered at the origin with radius 1.0
		float sphereSDF(vec3 p, float r) {
			return length(p) - r;
		}

		//Signed distance function for an XY aligned cylinder centered at the origin with height h and radius r.
		float cylinderSDF(vec3 p, float h, float r) {
			// How far inside or outside the cylinder the point is, radially
			float inOutRadius = length(p.xy) - r;
			
			// How far inside or outside the cylinder is, axially aligned with the cylinder
			float inOutHeight = abs(p.z) - h/2.0;
			
			// Assuming p is inside the cylinder, how far is it from the surface?
			// Result will be negative or zero.
			float insideDistance = min(max(inOutRadius, inOutHeight), 0.0);

			// Assuming p is outside the cylinder, how far is it from the surface?
			// Result will be positive or zero.
			float outsideDistance = length(max(vec2(inOutRadius, inOutHeight), 0.0));
			
			return insideDistance + outsideDistance;
		}

		//Signed distance function for a torus at origin p and with torus parameters t (radius, "thickness")
		float sdTorus(vec3 p, vec2 t)
		{
		    return length(vec2(length(p.xz) - t.x, p.y)) - t.y;
		}

		//Signed distance function describing the scene
		//Defines objects and how they change over time (uses the "iTime" (performance) variable from ShaderToy)
		float sceneSDF(vec3 samplePoint) {
			// Object Rotation in scene
			samplePoint = rotateX(iTime) * samplePoint;
			samplePoint = rotateY(iTime) * samplePoint;

			vec3 rotate1 = rotateX(iTime * 0.5) * samplePoint;
				 rotate1 = rotateY(iTime * 0.5) * rotate1;
			vec3 rotate2 = rotateX(iTime * 3.0) * samplePoint;
				 rotate2 = rotateY(iTime * 3.0) * rotate2;
			vec3 rotate3 = rotateX(iTime / 5.0) * samplePoint;
				 rotate3 = rotateY(iTime / 5.0) * rotate3;
			vec3 rotate4 = rotateX(iTime / 5.0) * samplePoint;
				 rotate4 = rotateY(iTime / 5.0) * rotate4;
			vec3 rotate5 = rotateX(iTime * 1.5) * samplePoint;
				 rotate5 = rotateY(iTime * 1.5) * rotate5;
			vec3 rotate6 = rotateX(iTime * 2.5) * samplePoint;
				 rotate6 = rotateY(iTime * 2.5) * rotate6;
			
			//protruding cylinders that form the cross
			float cylinderRadius = 0.2 + (1.0 + sin(2.2 * iTime)) / 2.0;
			float cylinder1 = cylinderSDF(samplePoint, 2.0, cylinderRadius);
			float cylinder2 = cylinderSDF(rotateX(radians(90.0)) * samplePoint, 2.0, cylinderRadius);
			float cylinder3 = cylinderSDF(rotateY(radians(90.0)) * samplePoint, 2.0, cylinderRadius);
			
			//cube is the "base" shape
			float cube = boxSDF(samplePoint * sin(0.2 * iTime), vec3(1.0, 1.0, 1.0));
			
			//sphere encompasses the cube at a certain point in time
			float sphere = sphereSDF(samplePoint, 0.5 + cos (1.5 * iTime));

			//torus turns around the center object
			float torus = sdTorus(rotate1, vec2(1.8, 0.05));
			
			//balls move in a pattern around the center object
			float ballOffset = 0.5 + 1.0 + sin(1.2);
			float ballRadius = abs(0.3*sin(1.2));
			float balls = 			sphereSDF(rotate1 - vec3(ballOffset, 0.0, 0.0), ballRadius);
			balls = unionSDF(balls, sphereSDF(rotate2 + vec3(ballOffset, 0.0, 0.0), ballRadius));
			balls = unionSDF(balls, sphereSDF(rotate3 - vec3(0.0, ballOffset, 0.0), ballRadius));
			balls = unionSDF(balls, sphereSDF(rotate4 + vec3(0.0, ballOffset, 0.0), ballRadius));
			balls = unionSDF(balls, sphereSDF(rotate5 - vec3(0.0, 0.0, ballOffset), ballRadius));
			balls = unionSDF(balls, sphereSDF(rotate6 + vec3(0.0, 0.0, ballOffset), ballRadius));
			
			//float csgNut = differenceSDF(intersectSDF(cube, sphere), unionSDF(cylinder1, unionSDF(cylinder2, cylinder3)));
			//float csgNut = differenceSDF(sphere, unionSDF(cylinder1, unionSDF(cylinder2, cylinder3)));
			float csgNut = intersectSDF(unionSDF(cylinder1, unionSDF(cylinder2, cylinder3)), unionSDF(cube, sphere));
			
			return unionSDF(unionSDF(balls, torus), csgNut);
		}

		//Function is similar to how view frustrum is calculated and encoded in the projection matrix
		vec3 rayDirection(float fieldOfView, vec2 size, vec2 fragCoord) {
	    	vec2 xy = fragCoord - size / 2.0;
	    	float z = size.y / tan(radians(fieldOfView) / 2.0);
	    	return normalize(vec3(xy, -z));
		}

		//Start from plane object (eye) and move in a specified direction until reaching scene surface and then return the distance
		float shortestDistanceToSurface(vec3 eye, vec3 marchingDirection, float start, float end) {
			float depth = start;
			for (int i = 0; i < MAX_MARCHING_STEPS; i++) {
				float dist = sceneSDF(eye + depth * marchingDirection);
				if (dist < EPSILON) {
					return depth;
				}
				depth += dist;
				if (depth >= end) {
					return end;
				}
			}
			return end;
		}

		//Find the gradient for intersecting points by getting the distance of nearby points (Epsilon distance away) and normalize points
		//Used to approximate lighting calculation
		vec3 estimateNormal(vec3 p) {
			return normalize(vec3(
				sceneSDF(vec3(p.x + EPSILON, p.y, p.z)) - sceneSDF(vec3(p.x - EPSILON, p.y, p.z)),
				sceneSDF(vec3(p.x, p.y + EPSILON, p.z)) - sceneSDF(vec3(p.x, p.y - EPSILON, p.z)),
				sceneSDF(vec3(p.x, p.y, p.z  + EPSILON)) - sceneSDF(vec3(p.x, p.y, p.z - EPSILON))
			));
		}

		//Lighting contribution of a single point light source via Phong illumination.
		//Input values: k_a: Ambient color, k_d: Diffuse color, k_s: Specular color, alpha: Shininess coefficient, p: position of point being lit, 
		//eye: the position of the camera, lightPos: the position of the light, lightIntensity: color/intensity of the light
		vec3 phongContribForLight(vec3 k_d, vec3 k_s, float alpha, vec3 p, vec3 eye, vec3 lightPos, vec3 lightIntensity) {
			vec3 N = estimateNormal(p);
			vec3 L = normalize(lightPos - p);
			vec3 V = normalize(eye - p);
			vec3 R = normalize(reflect(-L, N));
			
			float dotLN = dot(L, N);
			float dotRV = dot(R, V);
			
			if (dotLN < 0.0) {
				// Light not visible from this point on the surface
				return vec3(0.0, 0.0, 0.0);
			} 
			
			if (dotRV < 0.0) {
				// Light reflection in opposite direction as viewer, apply only diffuse
				// component
				return lightIntensity * (k_d * dotLN);
			}
			return lightIntensity * (k_d * dotLN + k_s * pow(dotRV, alpha));
		}

		//Lighting via Phong illumination.
		//Input values: k_a: Ambient color, k_d: Diffuse color, k_s: Specular color, alpha: Shininess coefficient, p: position of point being lit, 
		//eye: the position of the camera
		vec3 phongIllumination(vec3 k_a, vec3 k_d, vec3 k_s, float alpha, vec3 p, vec3 eye) {
			const vec3 ambientLight = 0.5 * vec3(1.0, 1.0, 1.0);
			vec3 color = ambientLight * k_a;
			
			vec3 light1Pos = vec3(4.0 * sin(iTime), 2.0, 4.0 * cos(iTime));
			vec3 light1Intensity = vec3(0.8, 0.8, 0.8);
			
			color += phongContribForLight(k_d, k_s, alpha, p, eye, light1Pos, light1Intensity);
			
			vec3 light2Pos = vec3(2.0 * sin(0.37 * iTime), 2.0 * cos(0.37 * iTime), 2.0);
			vec3 light2Intensity = vec3(0.8, 0.8, 0.8);
			
			color += phongContribForLight(k_d, k_s, alpha, p, eye, light2Pos, light2Intensity);   

			vec3 light3Pos = vec3(2.0, 6.0 * sin(0.67 * iTime), 6.0 * cos(0.67 * iTime));
			vec3 light3Intensity = vec3(0.8, 0.8, 0.8);
			
			color += phongContribForLight(k_d, k_s, alpha, p, eye, light3Pos, light3Intensity); 
			return color;
		}

		//Return a transform matrix that will transform a ray from view space to world coordinates
		//Input values: eye: the position of the camera, center: the camera target, up: up vector.
		mat4 viewMatrix(vec3 eye, vec3 center, vec3 up) {
			// Based on gluLookAt man page
			vec3 f = normalize(center - eye);
			vec3 s = normalize(cross(f, up));
			vec3 u = cross(s, f);
			return mat4(
				vec4(s, 0.0),
				vec4(u, 0.0),
				vec4(-f, 0.0),
				vec4(0.0, 0.0, 0.0, 1)
			);
		}
	            
		void main() {
			vec3 viewDir = rayDirection(45.0, u_resolution.xy, gl_FragCoord.xy);
	    	vec3 eye = vec3(8.0, 5.0, 7.0);
	    
			mat4 viewToWorld = viewMatrix(eye, vec3(0.0, 0.0, 0.0), vec3(0.0, 1.0, 0.0));
			
			vec3 worldDir = (viewToWorld * vec4(viewDir, 0.0)).xyz;
	    	float dist = shortestDistanceToSurface(eye, worldDir, MIN_DIST, MAX_DIST);
	    
	    	if (dist > MAX_DIST - EPSILON) {
	        	gl_FragColor = vec4(0.0, 0.0, 0.0, 0.0);
				return;
	    	}
	        
			vec3 p = eye + dist * worldDir;
			vec3 K_a = (estimateNormal(p) + vec3(0.0,0.0,0.0));
			vec3 K_d = K_a + vec3(0.1,0.2,0.3);
			vec3 K_s = vec3(1.0, 1.0, 1.0);
			float shininess = 20.0;

			vec4 c = texture2D(tex,vec2((gl_FragCoord.x/ u_resolution.x), (gl_FragCoord.y/ u_resolution.y)));
			vec3 color = phongIllumination(K_a, K_d, K_s, shininess, p, eye);
	    	gl_FragColor = vec4(color * c.rgb, 1.0);
		}
	</script>

	<!--Main Script-->
	<script>
		var container;
		var camera, scene, renderer, mesh;

		var vs = document.getElementById( 'vertexShader' ).textContent;
		var fs = document.getElementById( 'fragmentShader' ).textContent;

		init();
		animate();

		function init() {
			container = document.getElementById( 'container' );

			var width = window.innerWidth;
			var height = window.innerHeight;

			camera = new THREE.OrthographicCamera( width / -2, width / 2, height / 2, height / -2, 0.1, 1000 );
			camera.position.z = 10;

			scene = new THREE.Scene();

			//Geometry (For fullscreen quad)
			var geometry = new THREE.PlaneGeometry( width, height, 1, 1 );

			//Material and Texture (Extra Credit)
			var texture = new THREE.TextureLoader().load('sun.jpg');

			var uniforms =  {
				u_resolution: { type: "v2", value: new THREE.Vector2(width, height) },
				iTime: {typr: "f", value: 0.0},
				tex: { type: "t", value: texture },
			};

			var material = new THREE.RawShaderMaterial( {
				uniforms: uniforms,
				vertexShader: vs,
				fragmentShader: fs,
			} );

			mesh = new THREE.Mesh( geometry, material );
			scene.add( mesh );

			renderer = new THREE.WebGLRenderer();
			renderer.setSize( window.innerWidth, window.innerHeight );
			container.appendChild( renderer.domElement );

	        window.addEventListener( 'resize', onWindowResize, true );
		}

		function onWindowResize( event ) {
			camera.aspect = window.innerWidth / window.innerHeight;
			camera.updateProjectionMatrix();
			renderer.setSize( window.innerWidth, window.innerHeight );
			mesh.material.uniforms.u_resolution.value = new THREE.Vector2(window.innerWidth, window.innerHeight);
		}
		
		function animate() {
			requestAnimationFrame( animate );
			render();
		}

		function render() {
			var time = performance.now();
			mesh.material.uniforms.iTime.value = time* 0.0008;
			renderer.render( scene, camera );
		}

	</script>
	</body>
</html>
